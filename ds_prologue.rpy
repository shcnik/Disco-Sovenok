# DISCO SOVENOK
# ПРОЛОГ
label ds_prologue:
    $ backdrop = "prologue"
    $ new_chapter(-1, "Пролог")
    $ save_name = u"Disco Sovenok. Пролог."
    $ prolog_time()
    
    window show
    arb "Нет ничего. Лишь тьма. В ней пока что ещё теплится твоё сознание."
    arb "Всё, конец. Тебе больше ничего не нужно делать."
    arb "Никогда."
    arb "Совсем никогда."
    window hide
    menu:
        "Что происходит?":
            window show
            me "Что происходит?"
            arb "Агония. Ты умираешь. И ты умрёшь. Игра \"жизнь\" окончена."
            arb "Теперь у тебя не будет страданий."
            arb "Полностью."
            arb "Как тебе?"
            window hide
            menu:
                "Шикарно!":
                    window show
                    arb "А то!"
                "Я не хочу умирать!":
                    window show
                    arb "Пути назад нет. Всё кончено. Навеки."
        "Не существовать дальше":
            window show
    me "Ладно, пусть так..."
    window hide
    $ renpy.pause(3)
    window show
    arb "Проходит запредельное количество времени. Здесь нет борьбы. Здесь нет {i}бывших{/i}."
    window hide
    menu:
        "Не заметить":
            window show
            me "Давай ещё, ещё!"
            arb "Правильно понял! Ничего смешанное с ничем и политое ничем!"
            window hide
            $ renpy.pause(3.0)
            window show
        "Спросить":
            window show
            me "Кого-кого бывших?"
            lim "Осознание пробуждается в тебе... Из небытия пробуждается - и это ужасно! - ощущение. Ощущение горя, боли и смерти."
            lim "Это очень ужасный ход мыслей - и если ты продолжишь, то ощущение будет усиливаться."
            me "Так всё-таки, что там про бывших?"
            arb "Бывшая любовь. Бывшая нежность... Глупо возвращаться к этому. Оставайся здесь, здесь будет хорошо! Здесь нет страдания."
    play sound ds_sfx_psy
    vol "Нет, отставить! Ты должен выжить!"
    play sound ds_sfx_fys
    edr "У твоего тела ещё есть воля к жизни... Борись!"
    window hide
    menu:
        "Но зачем? Я не хочу больше жить!":
            window show
            arb "Правильно... ой нет! Что это?"
            arb "Где-то внутри тебя пробуждается ощущение. Ощущение реального мира."
            arb "Твоё сознание липнет к нему как муха к свету... Что-то тебя не отпускает, тянет обратно!"
            arb "Но... ты прекрасно знаешь..."
        "Да! Я должен бороться!":
            window show
            arb "Ой ли?"
            me "Да. Пусти меня!"
            lim "Ты не захочешь этого, если я расскажу тебе, что там происходит. Как ты думаешь - почему ты оказался тут?"
            me "И что меня ждёт?"
            lim "Огромный шар - и на нём злые обезьяны. Которые устроили на этом шаре махач. И ты - один из них."
            arb "И одно можно сказать точно..."
    arb "Твоя борьба бессмысленна."
    arb "Ты потерпел поражение."
    vol "Не сдавайся - ты же победитель!"
    window hide
    menu:
        "Забери меня обратно!":
            window show
            arb "Твоё тело решило иначе. Твоё тело решило, что нужно дальше цепляться за жизнь."
        "Нет, я хочу всё переиграть!":
            window show
            arb "Хочешь ещё один шанс?"
            arb "Хочешь вновь страдать, испытывать боль?"
            me "Да! Я смогу всё исправить!"
    # play sound ds_horn
    lim "Твой череп пронзает огненная полоса. Это звук. Очень резкий звук. Горн из ада..."
    window hide
    jump ds_day1
